build:
    @echo "Installing dependencies..."
    symfony composer install
    yes | phive install
    @echo "Installing Roadrunner..."
    yes | vendor/bin/rr get --location bin/

start:
    docker compose up -d
    bin/console cache:warmup
    nohup bin/rr serve -p -c .rr.dev.yaml --debug > /tmp/rr.out &
    
fix:
    vendor/bin/php-cs-fixer fix --diff
    
static-analysis output="default":
    vendor/bin/parallel-lint src public migrations config
    composer validate composer.json --no-check-publish
    bin/composer-require-checker --no-interaction
    bin/console lint:container -n {{ if output == "github" { "--format=github" } else { "" } }}
    bin/console lint:twig templates -n {{ if output == "github" { "--format=github" } else { "" } }}
    bin/console lint:yaml . *.yaml --parse-tags -n {{ if output == "github" { "--format=github" } else { "" } }}
    vendor/bin/php-cs-fixer fix --dry-run --diff
    vendor/bin/psalm {{ if output == "github" { "--output-format=github" } else { "" } }}
    
security-check:
    symfony security:check
    
test:
    echo "Running tests..."
    vendor/bin/phpunit